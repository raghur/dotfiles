# PROFILE values:
# 1 = zprof; 
# 2 = echo scripts sourced
local PROFILE=0
# see https://blog.mattclemente.com/2020/06/26/oh-my-zsh-slow-to-load/
[ $PROFILE -gt 0 ] && zmodload zsh/zprof
DISABLE_AUTO_UPDATE="true"
DISABLE_MAGIC_FUNCTIONS="true"
DISABLE_COMPFIX="true"
export PATH="$PATH:/home/$USER/bin:/home/$USER/.local/bin"
if  ! command -v sheldon > /dev/null; then
    curl --proto '=https' -fLsS https://rossmacarthur.github.io/install/crate.sh \
        | bash -s -- --repo rossmacarthur/sheldon --to ~/.local/bin
fi
export ZSH=~/.local/share/sheldon/repos/github.com/ohmyzsh/ohmyzsh
export ZSH_COMPDUMP=$ZSH/cache/.zcompdump-$HOST
setopt nullglob
setopt correct
unsetopt correctall

#ref: https://martinheinz.dev/blog/110
HISTFILE="$HOME/.zsh_history"
HISTSIZE=10000000
SAVEHIST=10000000
HISTORY_IGNORE="(l|l *|ls|ls *|cd|cd ..*|cd -|z *|pwd|exit|clear)"
setopt EXTENDED_HISTORY      # Write the history file in the ':start:elapsed;command' format.
setopt INC_APPEND_HISTORY    # Write to the history file immediately, not when the shell exits.
setopt SHARE_HISTORY         # Share history between all sessions.
setopt HIST_IGNORE_DUPS      # Do not record an event that was just recorded again.
setopt HIST_IGNORE_ALL_DUPS  # Delete an old recorded event if a new event is a duplicate.
setopt HIST_IGNORE_SPACE     # Do not record an event starting with a space.
setopt HIST_SAVE_NO_DUPS     # Do not write a duplicate event to the history file.
setopt HIST_VERIFY           # Do not execute immediately upon history expansion.
setopt APPEND_HISTORY        # append to history file (Default)
setopt HIST_NO_STORE         # Don't store history commands
setopt HIST_REDUCE_BLANKS    # Remove superfluous blanks from each command line being added to the history.
HIST_STAMPS="yyyy-mm-dd"

# set any config for plugins
export SHELDON_PROFILE=spaceship
ZSHZ_CASE=smart
ZSH_AUTOSUGGEST_STRATEGY=(history completion)
AGKOZAK_CUSTOM_SYMBOLS=( '⇣⇡' '⇣' '⇡' '+' 'x' '!' '>' '?' 'S')
AGKOZAK_CUSTOM_RPROMPT='[%D{%L:%M:%S %p}]'
AGKOZAK_LEFT_PROMPT_ONLY=1

# calculate hash
local hmac=${$(ls -lSt --full-time ~/.zsh.d/*.zsh|md5sum):/-/}
local uptodate=0
if [ -e ~/.zsh.d/library.$hmac.zwc ]; then
    uptodate=1
fi

fpath+=(~/.zsh.d/library.$hmac.zwc)
autoload -Uz ~/.zsh.d/*.zsh(D:t)
# hack for fash shell
#
# ~/.zsh.d/*.zsh 
#   - All files define functions only - so that return doesn't kill the parent shell
#   - To disable any file, move it to *.zsh.disabled
#
#   - Any file with _ is considered critical - so sourced in .zshrc.
#   - for other files - even early init like zoxide/mise/etc, the startup is delayed to the zle hook function below
#       when the enter key is pressed after a command
#       - see reset-prompt-and-accept-line in .zsh.d/_function.zsh for clean up routine
#
source $ZSH/oh-my-zsh.sh
eval "$(sheldon source)"
eval "$(starship init zsh)"


if [ $uptodate -eq 1 ]; then
    # fast path - when compiled sources exist
    # only execute the _script.zsh autoloaded functions
    for f in ~/.zsh.d/_*.zsh(D:t); do
        $f
        [ $PROFILE -ge 2 ] && echo "loaded $f"
    done
    # remaining are loaded in reset-prompt-and-accept-line widget
else
    # slow - source
    {
        # compile in background
        # https://stackoverflow.com/a/28370418
        echo "generating ~/zsh.d/library.$hmac.zwc"
        zcompile ~/.zsh.d/library.$hmac ~/.zsh.d/*.zsh
    } &!
    # source all .zsh files
    for f in ~/.zsh.d/_*.zsh; do
        source $f
        [ $PROFILE -ge 2 ] && echo "sourced: $f"
    done
fi

# now any final overrides
bindkey '^ ' autosuggest-accept
zstyle ':completion:*' menu select

bindkey '^g' clear-screen
zle -N reset-prompt-and-accept-line
bindkey '^m' reset-prompt-and-accept-line
bindkey "^[[1;5C" forward-word
bindkey "^[[1;5D" backward-word

start_tmux
[ $PROFILE -gt 0 ] && zprof
